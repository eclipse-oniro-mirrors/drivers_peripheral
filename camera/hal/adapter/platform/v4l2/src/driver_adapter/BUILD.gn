# Copyright (c) 2021 - 2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//drivers/hdf_core/adapter/uhdf2/uhdf.gni")
import("//drivers/peripheral/camera/hal/camera.gni")

config("v4l2_config") {
  visibility = [ ":*" ]

  cflags = [
    "-DGST_DISABLE_DEPRECATED",
    "-DHAVE_CONFIG_H",
    "-DCOLORSPACE=\"videoconvert\"",
  ]

  if (enable_camera_device_utest) {
    cflags += [
      "-fprofile-arcs",
      "-ftest-coverage",
    ]

    ldflags = [ "--coverage" ]
  }
}

ohos_shared_library("camera_v4l2_adapter") {
  sources = [
    "src/v4l2_buffer.cpp",
    "src/v4l2_control.cpp",
    "src/v4l2_dev.cpp",
    "src/v4l2_fileformat.cpp",
    "src/v4l2_stream.cpp",
    "src/v4l2_uvc.cpp",
  ]

  include_dirs = [
    "$camera_path/include",
    "//drivers/peripheral/camera/hal/adapter/platform/v4l2/src/driver_adapter/include",
    "//drivers/hdf_core/framework/include/utils",
    "//drivers/hdf_core/adapter/uhdf2/osal/include",
  ]

  if (is_standard_system) {
    external_deps = [
      "c_utils:utils",
      "hiviewdfx_hilog_native:libhilog",
    ]
  } else {
    external_deps = [ "hilog:libhilog" ]
  }
  external_deps += [ "drivers_interface_camera:metadata" ]
  public_configs = [ ":v4l2_config" ]
  install_images = [ chipset_base_dir ]
  subsystem_name = "hdf"
  part_name = "drivers_peripheral_camera"
}
