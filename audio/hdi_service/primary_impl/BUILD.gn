# Copyright (c) 2022-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

hdf_audio_path = "./../.."
hdf_hdi_service_path = "./.."
import("//build/ohos.gni")
import("//drivers/hdf_core/adapter/uhdf2/hdi.gni")
import("$hdf_audio_path/audio.gni")

ohos_shared_library("audio_primary_impl_vendor") {
  defines = []
  if (drivers_peripheral_audio_feature_community) {
    sources = [
      "src/audio_adapter.c",
      "src/audio_adapter_info_common.c",
      "src/audio_capture.c",
      "src/audio_common.c",
      "src/audio_manager.c",
      "src/audio_render.c",
    ]

    include_dirs = [
      "include",
      "$hdf_hdi_service_path/pathselect/include",
      "$hdf_audio_path/supportlibs/adm_adapter/include",
      "$hdf_audio_path/supportlibs/interfaces/include",
      "//third_party/bounds_checking_function/include",
      "$hdf_hdi_service_path/vendor_interface/utils",
    ]

    if (drivers_peripheral_audio_feature_hal_notsupport_pathselect) {
      defines += [ "AUDIO_HAL_NOTSUPPORT_PATHSELECT" ]
    }
  } else {
    sources = [
      "vendor_src/audio_adapter_vendor.c",
      "vendor_src/audio_capture_vendor.c",
      "vendor_src/audio_common_vendor.c",
      "vendor_src/audio_manager_vendor.c",
      "vendor_src/audio_render_vendor.c",
    ]

    include_dirs = [
      "vendor_include/include",
      "//third_party/bounds_checking_function/include",
      "$hdf_hdi_service_path/vendor_interface/utils",
    ]
  }

  if (enable_audio_hal_hdf_log) {
    defines += [ "AUDIO_HDF_LOG" ]
  }

  if (use_jemalloc && use_jemalloc_dfx_intf) {
    defines += [ "CONFIG_USE_JEMALLOC_DFX_INTF" ]
  }

  if (is_standard_system) {
    external_deps = [
      "c_utils:utils",
      "drivers_interface_audio:audio_idl_headers",
      "hdf_core:libhdf_ipc_adapter",
      "hdf_core:libhdi",
      "hdf_core:libpub_utils",
      "hiviewdfx_hilog_native:libhilog",
    ]
  } else {
    external_deps = [ "hilog:libhilog" ]
  }

  install_images = [ chipset_base_dir ]
  subsystem_name = "hdf"
  part_name = "drivers_peripheral_audio"
}
